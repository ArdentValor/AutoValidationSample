// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def libraryVersion = artifactMajorVersion + '.' + artifactMinorVersion + '.' + artifactBuildNumber

allprojects {
    repositories {
        jcenter()
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = groupName
            version = libraryVersion
            artifactId = projectName

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("autovalidation/build/outputs/aar/autovalidation-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                project(':autovalidation').configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                    dependencyNode.appendNode('scope', 'compile')
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://maven.hartsolutions.com:8081/artifactory'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = repoName
            username = repoUsername
            password = repoPassword
        }

        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            //properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
